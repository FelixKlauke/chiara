plugins {
  id "org.sonarqube" version "2.8"
  id 'com.github.johnrengelman.shadow' version '7.0.0'
}

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'
  apply plugin: 'com.github.johnrengelman.shadow'

  group = 'com.felixklauke.chiara'
  version = '3.1.0-SNAPSHOT'

  repositories {
    google()
    mavenLocal()
    mavenCentral()
    maven {
      url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
      url = "https://jitpack.io"
    }
    maven {
      url = "https://repo.codemc.org/repository/maven-public/"
    }
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
  }

  test {
    useJUnitPlatform()
  }

  sourceCompatibility = '11'

  java {
    withSourcesJar()
    withJavadocJar()
  }

  checkstyle {
    toolVersion '8.27'
    checkstyleTest.enabled = false
  }
}

subprojects {
  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/FelixKlauke/chiara")
        credentials {
          username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
          password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
      }
    }
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
  }
}

task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  subprojects.each {
    sourceSets it.sourceSets.main
  }

  reports {
    xml.enabled = true
    html.enabled = true
    xml.destination = new File("${buildDir}/reports/jacoco/report.xml")
    html.destination = new File("${buildDir}/reports/jacoco/report.html")
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "FelixKlauke_chiara"
  }
}
